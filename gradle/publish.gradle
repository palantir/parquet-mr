apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

task sourceJar(type: Jar) {
    from project.sourceSets.main.allSource
    classifier 'sources'
}

task testJar(type: Jar) {
    from project.sourceSets.test.output
    classifier 'tests'
}

build.dependsOn 'sourceJar', 'testJar'

publishing {
    publications {
        all {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile'}
            }
        }
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar)
            artifact(testJar)
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_PASSWORD')
    publish = true
    pkg {
        userOrg = 'palantir'
        repo = 'releases'
        name = 'parquet-mr'
        licenses = ['Apache-2.0']
        publications = ['bintray']
        vcsUrl = 'https://github.com/palantir/parquet-mr'
    }
}

artifactory {
    publish {
        contextUrl = 'https://oss.jfrog.org/artifactory'
        repository {
            repoKey = (project.version =~ /-/ ? 'oss-snapshot-local' : 'oss-release-local')
            username = System.env.BINTRAY_USER
            password = System.env.BINTRAY_KEY
            maven = true
        }
        defaults {
            publications (publishing.publications.bintray)
            properties = ['git': project.version.toString()]
        }
    }
}

artifactoryPublish.dependsOn 'generatePomFileForBintrayPublication', 'build'

ext {
    releaseVersionRegex = /[0-9]+(?:\.[0-9]+)+-palantir[0-9]+/
}

bintrayUpload.onlyIf {
    System.getenv('BINTRAY_USERNAME') && System.getenv('BINTRAY_PASSWORD') && project.version ==~ releaseVersionRegex
}

bintrayUpload.dependsOn 'generatePomFileForBintrayPublication', 'build'
