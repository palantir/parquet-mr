buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/groups/public" }
        jcenter()
    }
    dependencies {
        classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.9.0"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.0"
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:0.3.1"
        classpath "gradle.plugin.com.palantir.configurationresolver:gradle-configuration-resolver-plugin:0.1.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.4"
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.5.2"
    }
}

apply plugin: 'com.palantir.git-version'
apply plugin: 'idea'

allprojects {
    apply plugin: 'com.palantir.configuration-resolver'
    group = 'org.apache.parquet'
    version gitVersion()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    test {
        minHeapSize = "512m"
        maxHeapSize = "2048m"
    }

    repositories {
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://conjars.org/repo" }
        maven { url "http://scala-tools.org/repo-releases" }
        maven { url "https://repository.cloudera.com/artifactory/datanucleus" }
        maven { url "http://maven.twttr.com" }
    }

    if (System.env.CIRCLE_TEST_REPORTS) {
        test.reports.junitXml.destination = new File(System.env.CIRCLE_TEST_REPORTS, it.getName())
    }

    dependencies {
        testCompile 'junit:junit:4.10'
        testCompile 'org.easymock:easymock:3.2'
        testCompile 'commons-httpclient:commons-httpclient:3.0.1'
    }
}
