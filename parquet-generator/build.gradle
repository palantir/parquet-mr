description = 'Apache Parquet Generator'

def codegens = [
        'parquet-column' : 'org.apache.parquet.filter2.Generator',
        'parquet-common' : 'org.apache.parquet.version.Generator',
        'parquet-encoding': 'org.apache.parquet.encoding.Generator'
]

processResources {
    expand(version: version, buildNumber: 'abcdef12344')
}

codegens.each { entry ->
    def name = entry.getKey()
    def clz = entry.getValue()
    def taskName = "${name}Codegen"
    def cleanName = "${name}Clean"
    def outputPath = "$rootDir/${name}/src/generated/java"

    task "$taskName"(type: JavaExec) {
        dependsOn compileJava
        main clz
        args outputPath
        classpath sourceSets.main.runtimeClasspath
    }

    task "$cleanName"(type: Delete) {
        inputs.dir file('src/main/java')
        outputs.dir outputPath
        delete outputPath
    }

    tasks.clean.dependsOn cleanName
}

